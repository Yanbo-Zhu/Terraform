

E:\build\git\TerraFormIVUcloudAccounts_TFCLOUDACC\e2x\services\fitnesse_logs\main.tf

```hcl
#### ****************************************************************************************** ####
#### *                                      RESOURCES                                         * ####
#### ****************************************************************************************** ####

#### _____________________________________________________________ Backend configuration _____ ####

terraform {
  backend "s3" {
    profile        = "ivu-cloud-e31"
    region         = "eu-central-1"
    bucket         = "ivu-cloud-e2-tf-state-backend"
    encrypt        = true
    key            = "accounts/e2x/services/fitnesse_logs.tfstate"
    dynamodb_table = "e2-tf-state-backend"
  }
}

#### _____________________________________________________________ Provider configuration _____ ####

provider "aws" {  
  profile = local.aws_profile
  region  = local.region
}

locals {
  aws_profile        = "ivu-cloud-e2x"
  region             = "eu-central-1"
  bucket_name        = "ivu-cloud-e2x-fitnesse-logs"
}

#### ______________________________________________________________________ Module calls _____ ####

data "aws_caller_identity" "current" {}

# Bucket for local logging, for example for AWS ELBs.
resource "aws_s3_bucket" "fitnesse_logs" {
  bucket        = local.bucket_name
  force_destroy = true   # Allows deletion of the bucket even if it contains objects
  tags          = { "Name" = "${local.bucket_name}.s3" }
}


# Sets bucket ownership (BucketOwnerPreferred), which determines who owns uploaded objects.
# object_ownership = "BucketOwnerPreferred" means:
#    When someone uploads a file to this bucket, the bucket owner automatically becomes the owner of the file.
#    This is useful for avoiding permission issues when multiple accounts or users upload objects.
    
resource "aws_s3_bucket_ownership_controls" "fitnesse_logs_ownership" {
  bucket = aws_s3_bucket.fitnesse_logs.id
  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}



#  ACL (Access Control List)
# depends_on = [aws_s3_bucket_ownership_controls.fitnesse_logs_ownership] ensures that the ownership settings are applied before setting the ACL.
# acl = "private" means that only the bucket owner has access to the bucket and its objects.

resource "aws_s3_bucket_acl" "example" {
  depends_on = [aws_s3_bucket_ownership_controls.fitnesse_logs_ownership]

  bucket = aws_s3_bucket.fitnesse_logs.id
  acl    = "private"
}

resource "aws_s3_bucket_lifecycle_configuration" "fitnesse_logs_lifecycle" {
  bucket = aws_s3_bucket.fitnesse_logs.id
  rule {
    id = "90-day-cleanup.rule"
    status = "Enabled"
    expiration {
      days = 90
    }
  }
}


#  符合 identifiers 的 所有 identity  可以做 这些 actions,  on those resources 

data "aws_iam_policy_document" "put_for_instances" {
  statement {
    principals {
      type = "AWS"
      identifiers = ["arn:aws:iam::${data.aws_caller_identity.current.account_id}:role/default-instance.role"]
    }
    actions = [
      "s3:ListBucket",
      "s3:DeleteObject",
      "s3:GetObject*",
      "s3:PutObject*"
    ]
    resources = [
      aws_s3_bucket.fitnesse_logs.arn,
      "${aws_s3_bucket.fitnesse_logs.arn}/*"
    ]
  }
}

resource "aws_s3_bucket_policy" "fitnesse_logs_policy" {
  bucket = aws_s3_bucket.fitnesse_logs.id
  policy = data.aws_iam_policy_document.put_for_instances.json
}

```